* Alkaline Phosphate 
* ROB 6.20.2022
* STEP 4 = GSBP SETUP PT 3 - compute Mij matrix
* This could take a day or more. Luckily the loop over pol variable means 
* it can be continued from where it terminates. Just change pol to the first
* number in the sequence that needs to be done next and resubmit  
*

bomblev -2

!===============================================================================
!                          USER VARAIBLES
!===============================================================================
set topparstream = /projectnb/cui-buchem/rosadche/dnap/qm_mm/toppar.str

set pdbfile = SEDPDB
set protein = SEDPROT
set mg      = SEDMG
set state   = SEDSTATE

set sim     = gsbp_setup_pt3_mij
set this = @pdbfile_@protein_@{mg}mg_@state_@sim


if @sim .eq. gsbp_setup_pt3_mij then 
    set last = gsbp_setup_pt1
endif

set lastpsf = @pdbfile_@protein_@{mg}mg_@state_@last
set lastcor = @pdbfile_@protein_@{mg}mg_@state_@last



!===============================================================================
!                         TOPOLGY & PARAMETERS
!===============================================================================
stream @topparstream


!===============================================================================
!                         LOAD THE STRUCTURE(S)
!===============================================================================
open read unit 10 card name @psfdir/@lastpsf.psf
read psf  unit 10 card

open read unit 10 card name @cordir/@lastcor.cor
read coor unit 10 card


!===============================================================================
!                            SET UP SYSTEM
!===============================================================================
!-------SET UP DROPLET VARIABLES -----------------------------------------------
set xcen = 0.0
set ycen = 0.0
set zcen = 0.0


!-------SET UP GSBP VARIABLES -----------------------------------------------

set 1 23.0    ! radius for fully mobile region
set 2 27.0    ! radius of inner region
set 3 25.0    ! radius of buffer region where Langevin dynamics will be used

set Dcel_c  =   1.2     ! Size of Grid Unit Cell (Coarse)
set LEdge_c =   10.0     ! distance between a protein atom and edge of grid

set Dcel_f  =   0.4     ! Size of Grid Unit Cell (Fine)
set LEdge_f =   2.0     ! Distance b/w edge of focus region & grid

coor stat
calc xave = ( ?xmax + ?xmin ) / 2.0
calc yave = ( ?ymax + ?ymin ) / 2.0
calc zave = ( ?zmax + ?zmin ) / 2.0


!-------------------------------------------------------------------------------
! The following calcs will determine the grid spacing for the pbeq solver.
!-------------------------------------------------------------------------------
!-------------- COARSE GRID ----------------------------------------------------
calc Nclx_c = int ( ( @{LEdge_c} * 4.0 + ?Xmax - ?Xmin ) / @{Dcel_c} )
calc Ncly_c = int ( ( @{LEdge_c} * 4.0 + ?Ymax - ?Ymin ) / @{Dcel_c} )
calc Nclz_c = int ( ( @{LEdge_c} * 4.0 + ?Zmax - ?Zmin ) / @{Dcel_c} )

if @{Nclx_c} .gt. @{Ncly_c} then
    set Ncl_c_greatest = @{Nclx_c}
else 
    set Ncl_c_greatest = @{Ncly_c}
endif

if @{Ncl_c_greatest} .lt. @{Nclz_c} then
    set Ncl_c_greatest = @{Nclz_c}
endif

!-------------- FINE GRID ------------------------------------------------------
! The fine grid only matters for focused region so with spherical GSBP this is 
! the spherical inner region. In this case we make it only for diameter of sphere
! including a edge distance on both sides

calc Nclx_f = int ( 2.0 * ( @{LEdge_f} + @2 ) / @{Dcel_f} )
set Ncl_f_greatest = @{Nclx_f}

! If one were to ever use GSBP with inner region as rectangle, one would add an
! edge cutoff to each X, Y, and Z smaller iner rectangular box lengths


!-------------- GRID CHECK -----------------------------------------------------
! the LBOX LNCEL must be smaller or equal to NCEL of inner region
if @{Ncl_c_greatest} .gt. @{Ncl_f_greatest} then
    set Ncl_c_greatest = @{Ncl_f_greatest}
endif

!-------------------------------------------------------------------------------
!         DO THE GSBP MIJ CALCULATION 
!-------------------------------------------------------------------------------
! This calc takes a bit of time to do as you have to loop over several multipoles
! and the first few might have convergence issues. It's no problem. 

PBEQ

set pol     =   1       ! Should always start at 1 unless restarting
set maxpol  =   20      ! This number will be included in loop


!-------Loop begins here--------------------------------------------------------
label nextpol

reset                           !Need to reset grid before every loop...
prnlev 0                        !Then we need to reset radii
scalar wmain set 0.0
stream @gsbpdir/radius.str
prnlev 5

if @pol .ne. 1 then
    calc lastpol = @pol - 1
    open read file unit 50 name @gsbpdir/@this_@lastpol.dat
    read MIJ unit 50
    close unit 50
endif

! No hyperparmaters are hard coded in here
! If you want to override the ncel calulatiosn above, 
! just set the required variable by hand
GSBP G_ii SPHERE -
     NMPOL @pol MAXIter 200000   -
     SRDIST @2 RRXCEN @xcen RRYCEN @ycen RRZCEN @zcen - 
     EPSW 80.0 EPSP 1.0 NCEL @{Ncl_f_greatest} DCEL @{Dcel_f} INTBP NOSORT -
     XBCEN @xcen YBCEN @ycen ZBCEN @zcen - ! Center of basis functions
     LBOX FOCUS LNCEL @{Ncl_c_greatest} LDCEL @{Dcel_c} LXBCEN @xave LYBCEN @yave LZBCEN @zave

open write file unit 50 name @gsbpdir/@this_@pol.dat
write MIJ unit 50
close unit 50

incr pol by 1
if pol .le. @maxpol goto nextpol
!-------Loop ends here----------------------------------------------------------

END

